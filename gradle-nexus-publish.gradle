
buildscript {
	repositories{
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
	}
}

task uploadGradleScriptToNexus(type: DeployGradleToNexusTask, overwrite: true)

import static groovyx.net.http.Method.*
import groovyx.net.http.HTTPBuilder
import org.apache.http.entity.FileEntity

class DeployGradleToNexusTask extends DefaultTask {

	def nexusUrl = "$System.env.NEXUS_URL"
	def nexusPath = "$System.env.NEXUS_PATH"
	def nexusUsername = "$System.env.NEXUS_USERNAME"
	def nexusPassword = "$System.env.NEXUS_PASSWORD"
	def scriptName
	def scriptVersion
	def fileExtension = '.gradle'

	@TaskAction
	def uploadGradleScriptToNexus() {

		println "Uploading ${scriptName}${fileExtension} to nexus"

		def http = new HTTPBuilder(nexusUrl)
		def buildGradle = project.file("${scriptName}${fileExtension}")
		http.auth.basic nexusUsername, nexusPassword

		http.handler.failure = { response ->
			println "Unexpected failure: ${response.statusLine}"
			println response.getEntity().getContent().getText()
		}

		http.handler.success = { response ->
			println response.statusLine
			println "File ${scriptName}${fileExtension} successfully uploaded to nexus."
		}

		http.request(POST) { req ->
			uri.path = "${nexusPath}${scriptName}-${scriptVersion}${fileExtension}"
			req.entity = new FileEntity(buildGradle)
		}
	}
}
